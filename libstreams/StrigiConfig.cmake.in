# Generated by CMake, don't edit, changes will be lost.

set(STRIGI_VERSION_MAJOR @STRIGI_VERSION_MAJOR@ )
set(STRIGI_VERSION_MINOR @STRIGI_VERSION_MINOR@ )
set(STRIGI_VERSION_PATCH @STRIGI_VERSION_PATCH@ )

# all the following settings have been added November 16th, 2008
# before that this file was also installed but contained only the
# version information. 
# Alex

# this one is used in FindStrigi.cmake to test whether a recent enough
# version of StrigiConfig.cmake has been found
set(STRIGI_CONFIG_FOUND_AND_HAS_COMPLETE_INFORMATION TRUE)

# complete version string
set(STRIGI_VERSION ${STRIGI_VERSION_MAJOR}.${STRIGI_VERSION_MINOR}.${STRIGI_VERSION_PATCH} )

# information for the source incompat. change from 0.5.9 to 0.6.0
# No need to test for it, since here we know exactly what we have.
set(STRIGI_NEEDS_SIGNED_CHAR TRUE)
set(STRIGI_NEEDS_CHAR FALSE)

# get path of this file and go three levels up (this is equivalent to
# the CMAKE_INSTALL_PREFIX if LIB_DESTINATION is not set)
GET_FILENAME_COMPONENT(_IMPORT_PREFIX "${CMAKE_CURRENT_LIST_FILE}" PATH)
GET_FILENAME_COMPONENT(_IMPORT_PREFIX "${_IMPORT_PREFIX}" PATH)
GET_FILENAME_COMPONENT(_IMPORT_PREFIX "${_IMPORT_PREFIX}" PATH)
if(WIN32)
    GET_FILENAME_COMPONENT(_IMPORT_PREFIX "${_IMPORT_PREFIX}" REALPATH)
endif(WIN32)

# install locations
set(STRIGI_INCLUDE_DIR "${_IMPORT_PREFIX}/@INCLUDE_DESTINATION@")
set(STRIGI_LIBRARY_DIR "${_IMPORT_PREFIX}/@LIBRARY_DESTINATION@")

# find the full path to the libraries

# streamanalyzer
find_library(STRIGI_STREAMANALYZER_LIBRARY_RELEASE
  NAMES streamanalyzer
  PATHS ${STRIGI_LIBRARY_DIR} NO_DEFAULT_PATH 
)

find_library(STRIGI_STREAMANALYZER_LIBRARY_DEBUG
  NAMES streamanalyzerd
  PATHS ${STRIGI_LIBRARY_DIR} NO_DEFAULT_PATH 
)

# streams
find_library(STRIGI_STREAMS_LIBRARY_RELEASE
  NAMES streams
  PATHS ${STRIGI_LIBRARY_DIR} NO_DEFAULT_PATH 
)
find_library(STRIGI_STREAMS_LIBRARY_DEBUG
  NAMES streamsd
  PATHS ${STRIGI_LIBRARY_DIR} NO_DEFAULT_PATH 
)

# strigiqtdbusclient
find_library(STRIGI_STRIGIQTDBUSCLIENT_LIBRARY_RELEASE
  NAMES strigiqtdbusclient
  PATHS ${STRIGI_LIBRARY_DIR} NO_DEFAULT_PATH 
)
find_library(STRIGI_STRIGIQTDBUSCLIENT_LIBRARY_DEBUG
  NAMES strigiqtdbusclientd
  PATHS ${STRIGI_LIBRARY_DIR} NO_DEFAULT_PATH 
)

macro(_STRIGI_SET_LIBRARY_VAR _name)
   if(${_name}_RELEASE  AND  ${_name}_DEBUG)
      set(${_name} optimized "${${_name}_RELEASE}" debug "${${_name}_DEBUG}" )
   else(${_name}_RELEASE  AND  ${_name}_DEBUG)
      if(${_name}_RELEASE)
         set(${_name} "${${_name}_RELEASE}")
      else(${_name}_RELEASE)
         set(${_name} "${${_name}_DEBUG}" )
      endif(${_name}_RELEASE)
   endif(${_name}_RELEASE  AND  ${_name}_DEBUG)
endmacro(_STRIGI_SET_LIBRARY_VAR _name)

_strigi_set_library_var(STRIGI_STREAMANALYZER_LIBRARY)
_strigi_set_library_var(STRIGI_STREAMS_LIBRARY)
_strigi_set_library_var(STRIGI_STRIGIQTDBUSCLIENT_LIBRARY)

